plugins {
	// For some reason the jacoco aggregation doesn't work if we don't include the plugins here as well
	id 'org.springframework.boot' version '2.7.9'
	id 'io.spring.dependency-management' version '1.1.0'
	// This is needed to aggregate the jacoco report for the pipeline
	id 'base'
	id 'jacoco-report-aggregation'
}

// This will be applied to all projects, including :
allprojects {
	repositories {
		mavenCentral()
	}
}

reporting {
	reports {
		testCodeCoverageReport(JacocoCoverageReport) { 
			testType = TestSuiteType.UNIT_TEST
		}
	}
}

// This will be applied to all subprojects, so :client, :server and :commons
subprojects {
	// We don't seem to be able to use the plugins {...} syntax within subprojects {...}
	apply plugin: 'java'
	apply plugin: 'checkstyle'
	apply plugin: 'jacoco'
	
	group = 'oopp'
	version = '0.0.1-SNAPSHOT'

	checkstyle {
		toolVersion "10.9.2"
		ignoreFailures = false
		maxErrors = 0
		configFile = '../checkstyle.xml' as File
	}

	tasks.named('test') {
		useJUnitPlatform()
		finalizedBy jacocoTestReport 
	}
	
	jacocoTestReport {
		reports {
			xml.required = false
			csv.required = false
			html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
		}
	}
	
	dependencies {
		// Upgrading to v3.1.0 causes problems, particularly in the template project,
		// so does using 'javax.persistence:javax.persistence-api:2.2'
		implementation 'jakarta.persistence:jakarta.persistence-api:2.2.3'

		// This is used for some extensions to the core Java classes, mostly in the example code
		implementation 'org.apache.commons:commons-lang3:3.12.0'
		
		testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
		testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
		testImplementation 'org.mockito:mockito-core:5.2.0'
	}

	// Additional deprecation information
	tasks.withType(JavaCompile) {
		options.compilerArgs += ['-Xlint:deprecation']
	}
}

dependencies {
	jacocoAggregation project(':client') 
	jacocoAggregation project(':commons') 
	jacocoAggregation project(':server') 
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport) 
}